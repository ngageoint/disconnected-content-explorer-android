import java.util.zip.ZipEntry
import java.util.zip.ZipOutputStream

import static com.android.builder.model.AndroidProject.FD_INTERMEDIATES


apply plugin: 'com.android.application'


project.ext {
    archivesBaseName = 'dice'
}

android {
    compileSdkVersion 19
    buildToolsVersion '20.0.0'
    defaultConfig {
        applicationId 'mil.nga.dice'
        minSdkVersion 14
        targetSdkVersion 19
        testApplicationId 'mil.nga.dice.test'
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles(getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt')
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets {
        androidTest {
            resources {
                exclude 'test_reports/*/*'
            }
        }
    }
}


dependencies {
    compile 'com.android.support:support-v4:21.0.0'
    compile 'com.google.android.gms:play-services:6.1.11'
    compile files('libs/jackson-annotations-2.3.3.jar')
    compile files('libs/jackson-core-2.3.3.jar')
    compile files('libs/jackson-databind-2.3.3.jar')
    compile files('libs/jts-1.8.jar')

    androidTestCompile 'org.hamcrest:hamcrest-core:1.3'
}

task zipTestReports << {
    FileTree resTree = ((FileTree)android.sourceSetsContainer.androidTest.resources.sourceFiles).matching {
        it.include 'test_reports/*'
    }
    resTree.visit { FileVisitDetails reportRoot ->
        if (reportRoot.file.parent =~ /^.*test_reports/) {
            println "zipping test report from ${reportRoot.file.path}"
            buildZip reportRoot.file
        }
    }
}

project.tasks.whenTaskAdded { Task t ->
    if (t.name == 'processDebugTestJavaRes') {
        t.dependsOn zipTestReports
    }
}

def buildZip(File rootDir) {
    File destFile = new File("$project.buildDir/${FD_INTERMEDIATES}/javaResources/${android.testVariants[0].dirName}/${rootDir.name}.zip".toString());
    destFile.parentFile.mkdirs()
    destFile.createNewFile()
    try {
        ZipOutputStream zipOut = new ZipOutputStream(new FileOutputStream(destFile));
        zipOut.putNextEntry(new ZipEntry(rootDir.getName()));
        for (File file : rootDir.listFiles()) {
            zipOut.putNextEntry(new ZipEntry(rootDir.getName() + "/" + file.getName()));
            byte[] fileBytes = new byte[1 << 20];
            int readCount;
            FileInputStream fileIn = new FileInputStream(file);
            while ((readCount = fileIn.read(fileBytes, 0, fileBytes.length)) >= 0) {
                zipOut.write(fileBytes, 0, readCount);
            }
            zipOut.closeEntry();
        }
        zipOut.close();
        return destFile;
    }
    catch (IOException e) {
        throw new Error(e);
    }
}
